#===============================================================================
# Copyright 2019 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#===============================================================================

@PACKAGE_INIT@
set(DNNL_CPU_RUNTIME "@DNNL_CPU_RUNTIME@")
set(DNNL_CPU_THREADING_RUNTIME "@DNNL_CPU_THREADING_RUNTIME@")
set(DNNL_GPU_RUNTIME "@DNNL_GPU_RUNTIME@")

set(DNNL_BLAS_VENDOR "@DNNL_BLAS_VENDOR@")

set(DNNL_GPU_VENDOR "@DNNL_GPU_VENDOR@")

if(DNNL_CPU_THREADING_RUNTIME STREQUAL "TBB")
    # Try to find TBB using a TBB-provided CMake config file.
    find_package(TBB QUIET COMPONENTS tbb)
    # If the previous `find_package` call failed then try to
    # use a TBB CMake config file that is maintained by oneDNN.
    # The reason the previous call may fail is that TBB package is
    # very old and doesn't provide a CMake config file.
    if(NOT TBB_FOUND)
        message(STATUS "TBB-provided CMake config either failed or was not found. Trying to use a custom one.")
        # Use a custom find module for transitive dependencies
        set(DNNL_ORIGINAL_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})
        list(INSERT CMAKE_MODULE_PATH 0 ${PACKAGE_PREFIX_DIR}/@LIB_CONFIG_INSTALL_DIR@)
        find_package(TBB REQUIRED COMPONENTS tbb)
        # Reverting the CMAKE_MODULE_PATH to its original state
        set(CMAKE_MODULE_PATH ${DNNL_ORIGINAL_CMAKE_MODULE_PATH})
    endif()
    if(WIN32)
        # On Windows we must link to debug version of TBB library to ensure ABI compatibility
        # with MSVC debug runtime.
        set_property(TARGET TBB::tbb PROPERTY "MAP_IMPORTED_CONFIG_RELWITHMDD" "DEBUG")
    else()
        # On Linux TBB::tbb target may link to libtbb_debug.so which is not compatible with libtbb.so. Linking
        # application to both may result in undefined behavior.
        # See https://uxlfoundation.github.io/oneTBB/main/intro/limitations.html#debug-tbb-in-the-sycl-program
        set_property(TARGET TBB::tbb PROPERTY "MAP_IMPORTED_CONFIG_RELWITHMDD" "RELEASE" "NONE")
        set_property(TARGET TBB::tbb PROPERTY "MAP_IMPORTED_CONFIG_DEBUG" "RELEASE" "NONE")
    endif()
endif()

# Use a custom find module for transitive dependencies
set(DNNL_ORIGINAL_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})
list(INSERT CMAKE_MODULE_PATH 0 ${PACKAGE_PREFIX_DIR}/@LIB_CONFIG_INSTALL_DIR@)

if(DNNL_BLAS_VENDOR STREQUAL "ACCELERATE")
    set(BLA_VENDOR "Apple")
    find_package(BLAS REQUIRED)
    unset(BLA_VENDOR)
endif()

# Reverting the CMAKE_MODULE_PATH to its original state
set(CMAKE_MODULE_PATH ${DNNL_ORIGINAL_CMAKE_MODULE_PATH})

include("${CMAKE_CURRENT_LIST_DIR}/@LIB_EXPORT_NAME@.cmake")
check_required_components("@LIB_PACKAGE_NAME@")

if(DNNL_CPU_RUNTIME STREQUAL "SYCL" OR DNNL_CPU_RUNTIME STREQUAL "DPCPP" OR
   DNNL_GPU_RUNTIME STREQUAL "SYCL" OR DNNL_GPU_RUNTIME STREQUAL "DPCPP")
    if(DNNL_GPU_VENDOR STREQUAL "NVIDIA")
        set(DNNL_ORIGINAL_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})
        list(INSERT CMAKE_MODULE_PATH 0 ${PACKAGE_PREFIX_DIR}/@LIB_CONFIG_INSTALL_DIR@)
        find_package(cuDNN REQUIRED)
        find_package(cuBLAS REQUIRED)
        find_package(cublasLt REQUIRED)
        set(CMAKE_MODULE_PATH ${DNNL_ORIGINAL_CMAKE_MODULE_PATH})
    endif()
    set(DNNL_COMPILE_FLAGS "-fsycl")
    @HANDLE_BUNDLE_DEBUG_SYCL_CONFIGURATION@
endif()
